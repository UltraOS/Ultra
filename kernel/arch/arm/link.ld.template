ENTRY(arm_entry)

#ifdef ULTRA_ARCH_EXECUTION_MODE_AARCH32
VIRTUAL_BASE    = 0xC0000000;
PHYSICAL_OFFSET = 0;
#else
VIRTUAL_BASE    = 0xFFFFFFFF80000000;
PHYSICAL_OFFSET = 0;
#endif

#include <linker_common.ld.h>

// NOTE: These flags are currently not enforced by anyone
PHDRS
{
    text   PT_LOAD FLAGS(PHDR_READ | PHDR_EXEC);
    rodata PT_LOAD FLAGS(PHDR_READ);
    data   PT_LOAD FLAGS(PHDR_READ | PHDR_WRITE);
}

SECTIONS
{
    . = VIRTUAL_BASE + PHYSICAL_OFFSET;

    NORELOCS_GUARD

    .text : VIRTUAL_BASE_RELATIVE(.text)
    {
        TEXT_BEGIN
        TEXT
        TEXT_END
    } :text

    . = ALIGN(4K);

    .rodata : VIRTUAL_BASE_RELATIVE(.rodata)
    {
        RODATA(ULTRA_ARCH_WIDTH)
    } :rodata =0xDEADBEEF

    .eh_frame_hdr : VIRTUAL_BASE_RELATIVE(.eh_frame_hdr)
    {
        EH_FRAME_HDR
    } :rodata =0xDEADBEEF

    .eh_frame : VIRTUAL_BASE_RELATIVE(.eh_frame)
    {
        EH_FRAME
    } :rodata =0xDEADBEEF

    . = ALIGN(4K);

    .data : VIRTUAL_BASE_RELATIVE(.data)
    {
        DATA
    } :data

    .bss : VIRTUAL_BASE_RELATIVE(.bss)
    {
        BSS
    } :data

    SECTION_DEBUG
    SECTION_TABS

    . = ALIGN(8);

    /*
     * We don't use these for now but keep them anyway,
     * otherwise linker complains.
     */
	.ARM.exidx : {
		*(.ARM.exidx*)
	}
    .ARM.extab : {
        *(.ARM.extab*)
    }

    ARM.attributes 0x000000000 : {
        *(.ARM.attributes)
    }

    DISCARDS
}
