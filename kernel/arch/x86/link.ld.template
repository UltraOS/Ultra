ENTRY(x86_entry)

#ifdef ULTRA_ARCH_EXECUTION_MODE_I686
VIRTUAL_BASE    = 0xC0000000;
PHYSICAL_OFFSET = 0x100000;
#else
VIRTUAL_BASE    = 0xFFFFFFFF80000000;
PHYSICAL_OFFSET = 0;
#endif

#define INT3_OPCODE 0xCCCCCCCC

#include <linker_common.ld.h>

// NOTE: These flags are currently not enforced by anyone
PHDRS
{
    text   PT_LOAD FLAGS(PHDR_READ | PHDR_EXEC);
    rodata PT_LOAD FLAGS(PHDR_READ);
    data   PT_LOAD FLAGS(PHDR_READ | PHDR_WRITE);
}

SECTIONS
{
    . = VIRTUAL_BASE + PHYSICAL_OFFSET;

    NORELOCS_GUARD

    .text : VIRTUAL_BASE_RELATIVE(.text)
    {
        TEXT_BEGIN

        . = ALIGN(8);
        LINKER_SYMBOL(idt_thunks) = .;
        SPECIAL_SECTION(.text.idt_thunks)

        . = ALIGN(16);
        TEXT

        TEXT_END
    } :text =INT3_OPCODE

    . = ALIGN(4K);

    .rodata : VIRTUAL_BASE_RELATIVE(.rodata)
    {
        RODATA(ULTRA_ARCH_WIDTH)
    } :rodata =0xDEADBEEF

    .eh_frame_hdr : VIRTUAL_BASE_RELATIVE(.eh_frame_hdr)
    {
        EH_FRAME_HDR
    } :rodata =0xDEADBEEF

    .eh_frame : VIRTUAL_BASE_RELATIVE(.eh_frame)
    {
        EH_FRAME
    } :rodata =0xDEADBEEF

    . = ALIGN(4K);

    .data : VIRTUAL_BASE_RELATIVE(.data)
    {
        DATA
    } :data

    .bss : VIRTUAL_BASE_RELATIVE(.bss)
    {
        BSS
    } :data

    SECTION_DEBUG
    SECTION_TABS

    DISCARDS
}
